plugins {
    id 'com.github.lkishalmi.gatling' version '0.4.1'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    gatling 'org.scalaj:scalaj-http_2.11:2.3.0'
    gatling 'org.json4s:json4s-native_2.11:3.5.3'
    gatling 'com.amazonaws:aws-java-sdk-s3:1.11.185'
}

gatling {
    toolVersion = '2.2.5'
    jvmArgs = ['-server', '-Xms512M', '-Xmx512M', '-XX:MaxMetaspaceSize=512m']
    simulations = { include "**/*Simulation.scala" }
}

task('uploadReport', type: JavaExec) {
    main = 'com.vivareal.search.config.S3Uploader'
    args = ['build/reports/gatling', System.getenv('REPORT_VERSION') ?: 'SNAPSHOT']
    jvmArgs = System.properties.findAll { it.key.startsWith("gatling.") || it.key.startsWith("api.") || it.key.startsWith("aws.") }.collect { "-D$it" }
    classpath sourceSets.gatling.runtimeClasspath
    classpath configurations.gatling
}
uploadReport.mustRunAfter gatlingRun
