buildscript {
    ext {
        springBootVersion = '2.0.0.BUILD-SNAPSHOT'
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
    }
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

group = 'com.vivareal.search'
version = '0.0.1'

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = false
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'VivaReal Search API', 'Implementation-Version': version
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/snapshot" }
    mavenLocal()
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude module: 'undertow-websockets-jsr'
    newRelic
}

dependencies {
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-undertow'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile 'com.google.guava:guava:21.0'
    compile 'org.jparsec:jparsec:3.0'

    // compile 'org.elasticsearch.client:rest:5.1.2'
    compile 'org.elasticsearch.client:transport:5.3.0'
    compile 'org.elasticsearch:elasticsearch:5.3.0'

    compile 'org.glowroot:glowroot-agent-api:0.9.7'
    compile 'com.newrelic.agent.java:newrelic-api:3.37.0'

    compile 'org.apache.logging.log4j:log4j-web:2.6.2'
    compile 'org.slf4j:slf4j-api:1.7.21'

    newRelic 'com.newrelic.agent.java:newrelic-agent:' + NEW_RELIC_VERSION

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    // testCompile 'junit:junit:4.12'
    // testCompile 'org.springframework:spring-test:4.3.6.RELEASE'
}

task copyJarForDockerCompose(dependsOn: 'bootJar', type: Copy) {
    from jar
    into './build/libs/'
    rename { String fileName ->
        fileName = 'search-api.jar'
    }
}

task newrelic(dependsOn: 'bootJar') {
    doLast {
        copy {
            from configurations.newRelic.fileCollection { it.name == 'newrelic-agent' }.singleFile
            into './build/libs'
            rename { 'newrelic.jar' }
        }
    }

    if (project.hasProperty('newRelicKey')) {
        doLast {
            copy {
                filter(ReplaceTokens, tokens: [newRelicKey: newRelicKey])
                from 'src/main/resources/newrelic.yml'
                into './build/resources/main'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

bootRun {
    systemProperties = System.properties
}

build.finalizedBy copyJarForDockerCompose, newrelic

bootJar {
    launchScript {
        included = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "br.com.vivareal:search-api-v2"
        property "sonar.projectName", "search-api-v2"
        property "sonar.scm.provider", "git"
        property "sonar.host.url" , "http://sonarqube.vivareal.io/"
        property "sonar.sources", "src/main"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.tests", "src/test"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.login", "9eb3df2e079962a62dd40c39a9430795c856280e"
    }
}

